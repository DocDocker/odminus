
#Использовать irac

Процедура СформироватьБоковуюПанель(Знач АктивнаяСтрока = Неопределено)

	Панель = БоковаяПанельНавигации.ОписаниеПанели();

	ПараметрыСсылки = Новый Структура;
	ПараметрыСсылки.Вставить("controller","clusters");
	БоковаяПанельНавигации.ДобавитьСсылку(Панель, АдресМаршрута("ПоАгенту", ПараметрыСсылки), "Кластеры серверов");
	БоковаяПанельНавигации.ДобавитьСсылку(Панель, АдресДействия("index"), "Администраторы");

	Если АктивнаяСтрока <> Неопределено Тогда
		Панель[АктивнаяСтрока].Активность = Истина;
	КонецЕсли;

	ДанныеПредставления["Sidebar"] = Панель;

КонецПроцедуры

Функция Index() Экспорт

	Идентификатор = ЗначенияМаршрута["agent"];
	Если Идентификатор = Неопределено Тогда
		Возврат Перенаправление("/agents/index");
	КонецЕсли;

	Элемент = ЦентральныеСерверы.ПолучитьЭлемент(Идентификатор);
	Если Элемент = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	СформироватьБоковуюПанель(1);

	Администрирование = ОбщегоНазначения.ПолучитьАдминистрированиеКластера(
			Элемент.СетевоеИмя,
			Элемент.Порт);

	МодельСписка = Новый ПредставлениеСписка;
	МодельСписка.Колонки.Добавить("name");
	МодельСписка.Колонки.Добавить("descr");
	
	Если Сессия.Доступна Тогда
		Логин = Сессия.ПолучитьСтроку("АдминАгента"+Идентификатор);
		Пароль = Сессия.ПолучитьСтроку("ПарольАгента"+Идентификатор);
	Иначе
		Сообщить("Сессия недоступа", СтатусСообщения.Информация);
	КонецЕсли;

	Попытка
		Если Логин <> Неопределено Тогда
			Администрирование.УстановитьАдминистратора(Логин, Пароль);
		КонецЕсли;
		МодельСписка.Данные = Администрирование.Администраторы().Список();
		МодельСписка.Заголовок = "Администраторы агента кластера";
		МодельСписка.ПолеИдентификатора = "name";

		Возврат Представление("objectList", МодельСписка);
	Исключение
		Если ЭтоОшибкаАвторизации(ОписаниеОшибки()) Тогда
			Возврат Перенаправление(АдресДействия("Login"));
		Иначе
			Возврат Представление("rasError", ОписаниеОшибки());
		КонецЕсли;
	КонецПопытки;

КонецФункции

Функция Login() Экспорт
	
	Идентификатор = ЗначенияМаршрута["agent"];
	Если Идентификатор = Неопределено Тогда
		ПараметрыСсылки = Новый Структура;
		ПараметрыСсылки.Вставить("controller","clusters");
		Возврат Перенаправление(АдресМаршрута("ПоАгенту", ПараметрыСсылки));
	КонецЕсли;

	Если ЗапросHttp.Метод = "POST" Тогда
		Сессия.УстановитьСтроку("АдминАгента"+Идентификатор, ЗапросHttp.ДанныеФормы["userName"]);
		Сессия.УстановитьСтроку("ПарольАгента"+Идентификатор, ЗапросHttp.ДанныеФормы["password"]);
		Возврат Перенаправление(АдресДействия("Index"));
	Иначе
		СформироватьБоковуюПанель(1);
		МодельФормы = Новый Структура;
		МодельФормы.Вставить("Заголовок", "Авторизация на агенте");
		МодельФормы.Вставить("Действие", АдресДействия("Login"));
		Возврат Представление("loginForm", МодельФормы);
	КонецЕсли;

КонецФункции

Функция ЭтоОшибкаАвторизации(Знач Описание)
	Возврат Найти(Описание, "Ошибка операции администрирования") > 0;
КонецФункции