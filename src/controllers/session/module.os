#Использовать irac
#Использовать json

Функция Index() Экспорт
	
	ОбщегоНазначения.УстановитьЗаголовокСтраницы(ЭтотОбъект, "Сеансы");

	Кластер = ОбщегоНазначения.ПолучитьКластерПоИД(ЭтотОбъект);
	ИнформационнаяБаза_Идентификатор = ОбщегоНазначения.ПолучитьИдентификаторИнформационнойБазыИзПараметровЗапроса(ЭтотОбъект);

	ПараметрыМодели = Новый Структура;
	ПараметрыМодели.Вставить("Кластер_Идентификатор", ?(Кластер = Неопределено, "", Кластер.Ид()));
	ПараметрыМодели.Вставить("ИнформационнаяБаза_Идентификатор", ?(ИнформационнаяБаза_Идентификатор = Неопределено, "", ИнформационнаяБаза_Идентификатор));
	ПараметрыМодели.Вставить("СписокИнформационныхБаз", ОбщегоНазначения.ПолучитьСписокИнформационныхБазПоКластеру(ЭтотОбъект, Кластер));
	// ПараметрыМодели.Вставить("МодельДанных", ПолучитьМодельСписка());
	
	Возврат Представление("index", ПараметрыМодели);
	
КонецФункции

Функция GetModelDataList() Экспорт
	ПолучитьТолькоСтруктуру = ОбщегоНазначения.ПолучитьЗначениеПараметраЗапроса(ЭтотОбъект, "empty") <> Неопределено;
	Возврат Содержимое(ПолучитьДанныеДляСписка(ПолучитьТолькоСтруктуру));
КонецФункции

Функция CloseSession() Экспорт

	Метод = ЗапросHTTP.Метод;
	Если Метод = "POST" Тогда

		Ответ = "";

		Кластер = ОбщегоНазначения.ПолучитьКластерПоИД(ЭтотОбъект);		
		ИнформационнаяБаза_ИД = ОбщегоНазначения.ПолучитьЗначениеПараметраЗапроса(ЭтотОбъект, "db");
		ИнформационнаяБаза = ОбщегоНазначения.ПолучитьИнформационнуюБазуПоИд(ЭтотОбъект, ИнформационнаяБаза_ИД, Кластер);
		АвторизацияБазы = Новый АвторизацияИБ(ЭтотОбъект, Кластер, ИнформационнаяБаза);
		НомерСеанса = ОбщегоНазначения.ПолучитьЗначениеПараметраЗапроса(ЭтотОбъект, "session");
		РезультатПоискаСессии = ИнформационнаяБаза.Сеансы().Список(Новый Структура("Ид", НомерСеанса));
		Если РезультатПоискаСессии.Количество() > 0 Тогда		
			Сеанс = РезультатПоискаСессии[0]; 
			Попытка
				Сеанс.Завершить();
			Исключение
				// todo: есть подозрения, что завершение работает некорректно
			КонецПопытки;
			Ответ = "ok";
		Иначе
			Ответ = "data-not-found";
		КонецЕсли;

	Иначе
		Попытка
			ВызватьИсключение("Метод: " + Метод + " не поддерживается");
		Исключение
			Ответ = ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;

	Возврат Содержимое(Ответ);

КонецФункции

Функция ПолучитьДанныеДляСписка(ТолькоСтруктура)
	
	Возврат РаботаСЭлементамиФорм.ПолучитьДанныеИнформационнойБазыДляСписка(ЭтотОбъект, "session", ПолучитьМодельСписка(), ТолькоСтруктура);
	
КонецФункции

Функция ПолучитьМодельСписка()

	МодельСписка = Новый ПредставлениеСписка;

	МодельСписка.ПолеИдентификатора = "Ид"; 

	МодельСписка.Колонки.Добавить("НомерСеанса");
	МодельСписка.Колонки.Добавить("Пользователь");
	МодельСписка.Колонки.Добавить("Компьютер");
	МодельСписка.Колонки.Добавить("Приложение");
	МодельСписка.Колонки.Добавить("Язык");
	МодельСписка.Колонки.Добавить("ВремяНачала");
	МодельСписка.Колонки.Добавить("ПоследняяАктивность");
	МодельСписка.Колонки.Добавить("ЗаблокированоСУБД");
	МодельСписка.Колонки.Добавить("Action");

	// Действия
	МодельСписка.Действия.Добавить("terminate");

	Возврат МодельСписка;

КонецФункции