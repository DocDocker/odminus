#Использовать irac

Перем Панель;

Процедура СформироватьБоковуюПанель()

	Панель = БоковаяПанельНавигации.ОписаниеПанели();

	БоковаяПанельНавигации.ДобавитьСсылку(Панель, АдресДействия("index"), "Кластеры серверов");
	ПараметрыСсылки = Новый Структура;
	ПараметрыСсылки.Вставить("controller","agent-admins");
	БоковаяПанельНавигации.ДобавитьСсылку(Панель, АдресМаршрута("ПоАгенту", ПараметрыСсылки), "Администраторы");

	Панель[0].Активность = Истина;
	
	ДанныеПредставления["Sidebar"] = Панель;

КонецПроцедуры

Процедура СформироватьБоковуюПанельКластера()

	Панель = БоковаяПанельНавигации.ОписаниеПанели();

	БоковаяПанельНавигации.ДобавитьСсылку(Панель, АдресДействия("index"), "Кластеры серверов");
	
	ДобавитьСсылкуКластера("infobases","index","Информационные базы");
	ДобавитьСсылкуКластера("servers","index","Рабочие серверы");
	ДобавитьСсылкуКластера("rphosts","index","Рабочие процессы");
	ДобавитьСсылкуКластера("sessions","index","Сеансы");
	ДобавитьСсылкуКластера("agent-admins","index","Администраторы");

	Панель[0].Активность = Истина;
	
	ДанныеПредставления["Sidebar"] = Панель;

КонецПроцедуры

Процедура ДобавитьСсылкуКластера(Контроллер, Действие,Текст)
	ПараметрыСсылки = Новый Структура;
	ПараметрыСсылки.Вставить("controller", Контроллер);
	ПараметрыСсылки.Вставить("action"	 , Действие);
	БоковаяПанельНавигации.ДобавитьСсылку(Панель, АдресМаршрута("ПоАгенту", ПараметрыСсылки), Текст);
КонецПроцедуры

Функция Index() Экспорт

	Идентификатор = ЗначенияМаршрута["agent"];
	Если Идентификатор = Неопределено Тогда
		Возврат Перенаправление("/agents/index");
	КонецЕсли;

	Элемент = ЦентральныеСерверы.ПолучитьЭлемент(Идентификатор);
	Если Элемент = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	СформироватьБоковуюПанель();

	Попытка
		Администрирование = ОбщегоНазначения.ПолучитьАдминистрированиеКластера(
			Элемент.СетевоеИмя,
			Элемент.Порт,
			"8.3"
		);
		
		Кластеры = Администрирование.Кластеры();
		МодельПредставления = Новый Структура;
		МодельПредставления.Вставить("Агент", Элемент);
		МодельПредставления.Вставить("Кластеры", Кластеры.Список());
		Возврат Представление(МодельПредставления);
	Исключение
		Возврат Представление("rasError", ОписаниеОшибки());
	КонецПопытки
КонецФункции

Функция Overview() Экспорт
	
	// в версии 0.3 доступен метод ПараметрыЗапроса()
	// а пока - парсим сами

	СформироватьБоковуюПанельКластера();

	Параметры = ОбщегоНазначения.РазобратьПараметрыЗапроса(ЗапросHttp.СтрокаЗапроса);
	Кластер = Параметры["cluster"];
	Если Кластер = Неопределено Тогда
		Возврат Перенаправление("/");
	КонецЕсли;

	Возврат Представление(, Кластер);

КонецФункции