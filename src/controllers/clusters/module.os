#Использовать irac

Функция Index() Экспорт

	Идентификатор = ЗначенияМаршрута["agent"];
	Если Идентификатор = Неопределено Тогда
		Возврат Перенаправление("/clusters/index");
	КонецЕсли;

	Элемент = ЦентральныеСерверы.ПолучитьЭлемент(Идентификатор);
	Если Элемент = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	Попытка
		Администрирование = ОбщегоНазначения.ПолучитьАдминистрированиеКластера(
			Элемент.СетевоеИмя,
			Элемент.Порт,
			"8.3"
		);
		
		Кластеры = Администрирование.Кластеры();
		МодельПредставления = Новый Структура;
		МодельПредставления.Вставить("Агент", Элемент);
		МодельПредставления.Вставить("Кластеры", Кластеры.Список());
		Возврат Представление(МодельПредставления);
	Исключение
		Возврат Представление("rasError", ОписаниеОшибки());
	КонецПопытки
КонецФункции

Функция Edit() Экспорт

	Идентификатор = ЗначенияМаршрута["agent"];
	Если Идентификатор = Неопределено Тогда
		Возврат Перенаправление("/clusters/index");
	КонецЕсли;

	Элемент = ЦентральныеСерверы.ПолучитьЭлемент(Идентификатор);
	Если Элемент = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	Попытка
		Администрирование = ОбщегоНазначения.ПолучитьАдминистрированиеКластера(
			Элемент.СетевоеИмя,
			Элемент.Порт,
			"8.3"
		);
		
		Кластеры = Администрирование.Кластеры();
		Отбор = Новый Соответствие;
		Отбор.Вставить("Ид",ЗначенияМаршрута["id"]);
		Кластеры = Администрирование.Кластеры().Список(Отбор);
		Если Кластеры.Количество() = 0 Тогда
			Возврат КодСостояния(404);
		КонецЕсли;

		Если ЗапросHttp.Метод = "POST" Тогда
			ПроверитьЗаполнение();
			Если СостояниеМодели.Корректно Тогда
				ОбновитьКластер(Кластеры[0]);
				Возврат Представление("Index");
			КонецЕсли;
		КонецЕсли;

		Возврат Представление(Кластеры[0]);
		
	Исключение
		Возврат Представление("rasError", ОписаниеОшибки());
	КонецПопытки

КонецФункции

Процедура ПроверитьЗаполнение()
	ДанныеФормы = ЗапросHttp.ДанныеФормы;

	Если Не ЗначениеЗаполнено(ДанныеФормы["Имя"]) Тогда
		СостояниеМодели.ДобавитьОшибку("Имя", "Не заполнено имя кластера");
	КонецЕсли;

КонецПроцедуры

Процедура ОбновитьКластер(Знач Кластер)
	ДанныеФормы = ЗапросHttp.ДанныеФормы;
	НовоеИмя = ДанныеФормы["Имя"];
	Если НовоеИмя = Кластер.Имя() Тогда
		НовоеИмя = "";
	КонецЕсли;

	Кластер.Изменить(НовоеИмя, ДанныеФормы);

КонецПроцедуры