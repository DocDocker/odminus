#Использовать irac

Перем Авторизация;
Перем АдминистрированиеКластера;

Функция Авторизовать()

	ПараметрыДоступа = АдминистрированиеАгента.Авторизовать(ЭтотОбъект);
	Если ПараметрыДоступа = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	Авторизация = ПараметрыДоступа.Авторизация;
	АдминистрированиеКластера = ПараметрыДоступа.АдминистрированиеКластера;

	Возврат Истина;

КонецФункции

Функция Index() Экспорт

	Если Не Авторизовать() Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	Попытка
		Кластеры = АдминистрированиеКластера.Кластеры();
		МодельПредставления = Новый Структура;
		МодельПредставления.Вставить("Агент", Авторизация.ИдентификаторАгента());
		МодельПредставления.Вставить("Кластеры", Кластеры.Список());
		Возврат Представление(МодельПредставления);
	Исключение
		Возврат Авторизация.ОбработкаОшибкиАвторизации(ОписаниеОшибки());
	КонецПопытки
КонецФункции

Функция Edit() Экспорт

	Если Не Авторизовать() Тогда
		Возврат КодСостояния(404);
	КонецЕсли; 

	Попытка
		Отбор = Новый Соответствие;
		Отбор.Вставить("Ид",ЗначенияМаршрута["id"]);
		Кластеры = АдминистрированиеКластера.Кластеры().Список(Отбор);
		Если Кластеры.Количество() = 0 Тогда
			Возврат КодСостояния(404);
		КонецЕсли;

		Если ЗапросHttp.Метод = "POST" Тогда
			ПроверитьЗаполнение();
			Если СостояниеМодели.Корректно Тогда
				ОбновитьКластер(Кластеры[0]);
				Возврат ПеренаправлениеНаДействие("Index");
			КонецЕсли;
		КонецЕсли;

		Возврат Представление(Кластеры[0]);
		
	Исключение
		Возврат Авторизация.ОбработкаОшибкиАвторизации(ОписаниеОшибки());
	КонецПопытки

КонецФункции

Процедура ПроверитьЗаполнение()
	ДанныеФормы = ЗапросHttp.ДанныеФормы;

	Если Не ЗначениеЗаполнено(ДанныеФормы["Имя"]) Тогда
		СостояниеМодели.ДобавитьОшибку("Имя", "Не заполнено имя кластера");
	КонецЕсли;

КонецПроцедуры

Процедура ОбновитьКластер(Знач Кластер)
	ДанныеФормы = ЗапросHttp.ДанныеФормы;
	НовоеИмя = ДанныеФормы["Имя"];
	Если НовоеИмя = Кластер.Имя() Тогда
		НовоеИмя = "";
	КонецЕсли;

	ПараметрыКластера = Новый Структура;

	Для Каждого КлючИЗначение Из ДанныеФормы Цикл
		Сообщить(КлючИЗначение.Ключ + "=" + КлючИЗначение.Значение, СтатусСообщения.Внимание);
		ПараметрыКластера.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	Кластер.Изменить(НовоеИмя, ПараметрыКластера);

КонецПроцедуры