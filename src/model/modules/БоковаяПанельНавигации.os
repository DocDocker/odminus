
Функция ОписаниеПанели() Экспорт

	Описание = Новый ДеревоЗначений;
	Описание.Колонки.Добавить("Тип");
	Описание.Колонки.Добавить("Активность",Новый ОписаниеТипов("Булево"));
	Описание.Колонки.Добавить("Адрес"     ,Новый ОписаниеТипов("Строка"));
	Описание.Колонки.Добавить("Текст"     ,Новый ОписаниеТипов("Строка"));
	Описание.Колонки.Добавить("Картинка"  ,Новый ОписаниеТипов("Строка"));

	Возврат Описание;

КонецФункции

Функция ДобавитьСсылку(Знач Описание, Знач Адрес, Знач Текст, Знач Картинка = Неопределено, Знач Активна = Ложь) Экспорт
	Стр = Описание.Строки.Добавить();
	Стр.Тип = ТипыЭлементовПанели().Ссылка;
	Стр.Адрес =Адрес;
	Стр.Текст = Текст;
	Стр.Картинка = Картинка;
	Стр.Активность = Активна;
	Возврат Стр;
КонецФункции

Функция ТипыЭлементовПанели() Экспорт
	Возврат Новый Структура("Ссылка,Разделитель", "Ссылка","Разделитель");
КонецФункции

Функция СформироватьНавигацию(Знач Маршрутизатор) Экспорт
	
	Описание = ОписаниеПанели();

	ПутьТекущегоЗапроса = Маршрутизатор.ЗапросHttp.Путь + Маршрутизатор.ЗапросHttp.СтрокаЗапроса;
	
	Если Маршрутизатор.ЗначенияМаршрута["controller"] = "agent-admins" Или Маршрутизатор.ЗначенияМаршрута["controller"] = "clusters" Тогда
		ДобавитьСсылкуАгента(Описание, Маршрутизатор, "clusters", "Кластеры серверов", "mif-cloud", ПутьТекущегоЗапроса);
		ДобавитьСсылкуАгента(Описание, Маршрутизатор, "agent-admins", "Администраторы", "fas fa-user-shield",ПутьТекущегоЗапроса);
	Иначе
		Кластер = ИдентификаторКластера(Маршрутизатор.ЗапросHttp);
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "cluster","Обзор кластера", "fas fa-chart-line", ПутьТекущегоЗапроса,"overview");
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "infobase","Информационные базы", "fas fa-database", ПутьТекущегоЗапроса);
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "server","Рабочие серверы", "fas fa-server", ПутьТекущегоЗапроса);
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "admins","Администраторы", "fas fa-user-shield", ПутьТекущегоЗапроса);
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "manager","Менеджеры кластера", "mif-home", ПутьТекущегоЗапроса);
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "process","Рабочие процессы", "fas fa-cogs", ПутьТекущегоЗапроса);
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "session","Сеансы", "fas fa-users",ПутьТекущегоЗапроса);
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "connection","Соединения", "fas fa-plug",ПутьТекущегоЗапроса);
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "lock","Блокировки", "fas fa-unlock-alt",ПутьТекущегоЗапроса);
		ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, "profile","Профили безопасности", "fas fa-shield-alt", ПутьТекущегоЗапроса);
	КонецЕсли;

	Возврат Описание;

КонецФункции

Процедура ДобавитьСсылкуАгента(Описание, Маршрутизатор, ИмяКонтроллера, Текст, Картинка, ПутьТекущегоЗапроса)
	ПараметрыСсылки = Новый Структура;
	ПараметрыСсылки.Вставить("controller",ИмяКонтроллера);
	Строка = БоковаяПанельНавигации.ДобавитьСсылку(Описание, Маршрутизатор.АдресМаршрута("ПоАгенту", ПараметрыСсылки), Текст, Картинка);
	Если Строка.Адрес = ПутьТекущегоЗапроса Тогда
		Строка.Активность = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьСсылкуКластера(Описание, Кластер, Маршрутизатор, ИмяКонтроллера, Текст, Картинка, ПутьТекущегоЗапроса, ИмяДействия = "index")
	
	ПараметрыСсылки = Новый Структура;
	ПараметрыСсылки.Вставить("controller",ИмяКонтроллера);
	ПараметрыСсылки.Вставить("cluster"   ,Кластер);
	ПараметрыСсылки.Вставить("action"    ,ИмяДействия);
	Строка = ДобавитьСсылку(Описание, Маршрутизатор.АдресМаршрута("ПоАгенту", ПараметрыСсылки), Текст, Картинка);
	Если Строка.Адрес = ПутьТекущегоЗапроса Тогда
		Строка.Активность = Истина;
	КонецЕсли;

КонецПроцедуры

Функция ИдентификаторКластера(ЗапросHttp)
	Параметры = ЗапросHttp.ПараметрыЗапроса();
	Возврат Параметры["cluster"];
КонецФункции